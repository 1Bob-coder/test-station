pause 5

sendln ""
sendln ""

timeout = 90   ; switch to 90 second timeout

; nfs mount the drive
sendln "mount 10.77.2.235:/extra/nfs/mgi1788/dsr8xx /mnt/nfs "
sendln ""
wait "#"
sendln "ls /mnt/nfs"
wait "packages"

if result = 0 then
	gettitle titletext                ; get the title
	sprintf2 message "Not able to nfs mount %s" titletext
	messagebox message "Error"
	goto error
endif

pause 2
sendln ""
pause 1

; Looking for something of the form 'DSR830 sprint02 70.04'
sendln "ver"
wait "DSR830" "DSR800"    ; wait for one of these two strings

; Call the package_loader with the package to flash
if result = 1 then
	sendln "package_loader /mnt/nfs/packages/package_DSR830.bin"
elseif result=2 then
	sendln "package_loader /mnt/nfs/packages/package_DSR800.bin"
else
	gettitle titletext                ; get the title
	sprintf2 message "Not able to flash %s" titletext
	messagebox message "Error"
	goto error
endif

timeout = 120  ; switch to 2 minute timeout

; Wait till finished, then reboot.
waitrecv 'PL_FlashImage:FINISHED' 22 1
sendln "reboot"

; Wait for box to boot up, then get the version of the code.
; Save the version to a log file.
wait "SYS INIT FINISHED"          ; wait for text
pause 60                          ; wait 60 seconds
gettime timestr "%Y%m%d-%H%M%S"   ; get the date and time
gettitle titletext                ; get the title
sprintf2 filename 'C:\Users\dsr\Documents\TestCenter\logs\%s_ver_%s.log' titletext timestr
logopen filename 0 0              ; open a logfile
timeout = 10
pause 5
sendln "ver"
waitrecv 'sprint' 6 1
pause 2
logclose                          ; close the logfile

; send memory command ?
; clear debug log files ?

:error

; Close TeraTerm
closett

end

