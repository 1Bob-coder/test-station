; Purpose: TeraTerm Macro for flashing the box.
; Note: This makes use of Shaw's package_loader command.
; Modify the mount_command as needed.
;mount_command = "mount 10.73.76.57:/extra/nfs/mgi1788/regressiontest /mnt/nfs "
mount_command = "mount 10.73.76.57:/extra/nfs/mgi1788/dsr8xx /mnt/nfs "
package_DSR800 = "package_loader /mnt/nfs/packages/package_DSR800.bin"
package_DSR830 = "package_loader /mnt/nfs/packages/package_DSR830.bin"


timeout = 10   ; "wait" commands have 10 second timeout
nfs_count = 0
reboot_count = 0
box_is_flashed = 0

pause 1

sendln ""
pause 1
sendln ""
pause 1

:nfs_mount
nfs_count = nfs_count + 1
; nfs mount the drive
sendln mount_command
sendln ""
wait "#"

if result = 0 then
	; Not getting the prompt.  Try to nfs mount the drive again.
	if nfs_count < 2 goto nfs_mount
	if reboot_count = 0 goto reboot		; Sometimes the box needs to be rebooted to nfs mount the drive.

	gettitle titletext                ; get the title
	sprintf2 message "Not getting prompt #, unable to nfs mount %s" titletext
	messagebox message "Error"
	goto error
endif

timeout = 60	; switch to 60 second timeout

sendln ""
pause 1
sendln "ls /mnt/nfs"
wait "packages"

if result = 0 then
	; Not getting a list of packages.  Try to nfs mount the drive again.
	if nfs_count < 2 goto nfs_mount
	if reboot_count = 0 goto reboot		; Sometimes the box needs to be rebooted to nfs mount the drive.

	gettitle titletext                ; get the title
	sprintf2 message "Not able to nfs mount %s" titletext
	messagebox message "Error"
	goto error
endif

timeout = 120   ; switch to 120 second timeout

pause 2
sendln ""
pause 1

; Looking for something of the form 'DSR830 sprint02 70.04'
sendln "ver"
wait "DSR830" "DSR800"    ; wait for one of these two strings

; Call the package_loader with the package to flash
if result = 1 then
	sendln package_DSR830
elseif result=2 then
	sendln package_DSR800
else
	gettitle titletext                ; get the title
	sprintf2 message "Not able to flash %s" titletext
	messagebox message "Error"
	goto error
endif
box_is_flashed = 1		; Mark that the box is flashed

timeout = 200   ; switch to 200 second timeout

; Wait till finished, then reboot.  PL_FlashImage:FINISHED
waitrecv 'PL_FlashImage:FINISHED' 22 1
:reboot
pause 2
sendln ""
pause 1
sendln "reboot"
pause 2

; Wait for box to boot up, then get the version of the code.
wait "SYS INIT FINISHED"          ; wait for text
pause 60                          ; wait 60 seconds
sendln "ver"
pause 5
reboot_count = reboot_count + 1
if box_is_flashed < 1 goto nfs_mount

; Close TeraTerm
closett
:error
end

